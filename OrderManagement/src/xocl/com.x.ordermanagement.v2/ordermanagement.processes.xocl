/** This file includes the processes that are part of the
 *  order management domain
 */
package com.x.ordermanagement.v2 {
	import com.x.ordermanagement.v2.*
	import com.x.customer.v1.*
	
	entity Order {
    	state Submitted[Cancelled, Shipped] "Order has been submitted. The next states are either Cancelled or Shipped."
    	state Shipped[Cancelled, Returned] "Order has shipped. The next states are either Cancelled or Returned."
    	state Cancelled "Order has been Cancelled."
    	state Returned "Order has been returned."
    }
    
    // A new process for marketing channels and analytics.  Non-PII only, no customer information.  
    // Search only for now
    process OrderSearch<NonSensitive, MarketplaceOrderSearch> {
    	
    	summary "Searches for orders based on the search criteria provided."
    	description "A marketing or analytics provider searches for orders based on the search criteria provided."
    	
    	roles {
    		OrderManager "Searches for modified orders." //?? only modified orders
    		OrderConsumer "Submits queries to retrieve orders that have been modified since a specified timestamp." //?? do they received the results?
    		
    	}
    	
    	workflow {
    		    		
    		summary "Searches for orders."
    		description "Searches for orders."
    		
    		transaction SearchOrder<NonSensitive, MarketplaceOrderSearch> {
    			roles (OrderManager, OrderConsumer)
    		}   
    	}
    }
    
    
    // Submit an order that is in an already shipped state
    process SubmitShippedOrder<MarketplaceOrder>(Order order) {
    	
    	summary "Submits an order to the processor that has already been shipped."
    	description "Submits an order to the OrderProcessor that has already been shipped." 
    	
    	roles {
    		
    		SalesChannel "Submits an order to the order processor." 
    		OrderProcessor "Becomes aware of an order from a sales channel."
    	}
    	
    	workflow {
    		
    		summary "Submits an order to the OrderProcessor that has already been shipped."
    		description "Submits an order to the OrderProcessor that has already been shipped."
    		
    		transaction SubmitOrder<MarketplaceOrder> {
    			roles (SalesChannel, OrderProcessor)
    		}   
    	}
    }
    
    // Process orders that originate from a sales channel
    process ProcessSalesChannelOrder<MarketplaceOrder, MarketplacePaymentUpdate>(Order order) {
    	
    	summary "Processes an order."
        description "An order is submitted and is processed by making adjustments, changing the status, etc."
        
        roles {
        	SalesChannel "Submits the order for processing."
        	OrderProcessor "Performs the order processing."
        	OrderObserver "Informed of the processing that happened."
        	CustomerManager "Supplies the details needed for the processing of the order."
        
        }
    
	    workflow {
	    	    		
    		summary "Processes an order."
    		description "Processes an order."
	    	
	    	// A new order may already have arrived at the OrderProcessor
	    	// via a separate flow (SearchOrder, for example). That means the 
	    	// ProcessOrder transaction may not be required to start the workflow.
	        any {
		        // An order submitter submits an order to an order processor
	       	 	transaction ProcessOrder<MarketplaceOrder> {
	        		roles(SalesChannel, OrderProcessor)
	        	}
	        }
        	
	        any {
	        	workflow CustomerUpsert {
	        		    		
    				summary "Adds or creates a customer." 
    				description "Adds or creates a customer."
	        		
	    			roles(OrderProcessor, CustomerManager)
	    		}
	    	}
	    	
   			// Notify observers that an order was created
       	    transaction OrderCreated {
        		roles(SalesChannel, OrderObserver)
       		}
       		
	    	
	    	do (!order[Order.Cancelled] && !order[Order.Shipped] && !order[Order.Returned]) {
	    		any {
	    			noop //TN: Noop added to allow for polling semantic	
		    		transaction ShippingInformationUpdate<MarketplaceOrder> {
		    			roles(OrderProcessor, SalesChannel)
		    		}
		    		
		    		transaction OrderAdjust {
		    			roles(OrderProcessor, OrderObserver)
		    		}
		    		
		    		transaction OrderReturn {
		    			roles(OrderProcessor, OrderObserver)
		    		}
		    	
		    		transaction PartialOrderReturn {
		    			roles(OrderProcessor, OrderObserver)
		    		}
		    		
		    		transaction PaymentUpdate<MarketplacePaymentUpdate> {
		    			roles(SalesChannel, OrderProcessor)
		    		}
		    		
		        	all {  
			        	// An order processor updates shipment tracking information for an order
			        	transaction OrderShipped<MarketplaceOrder> {
			        		roles(OrderProcessor, SalesChannel)
			        	}
			        	
			        	transaction ObserveOrderShipped<MarketplaceOrder> {
			        		roles(OrderProcessor, OrderObserver)
			        	}
			        }
	        	
		        	any {
			        	do {
							// A canceller asks the processer to cancel its order 
							transaction CancelOrder<MarketplaceOrder> {
							   	roles(SalesChannel, OrderProcessor)
							}
							workflow OrderCancelled<MarketplaceOrder> {
						   		roles(OrderProcessor, OrderObserver)
						   	}
						}
						
						// The order processor informs observers that an order was cancelled
						workflow OrderCancelled<MarketplaceOrder> {
						   	roles(OrderProcessor, OrderObserver)
						}
					}
				}
			}
		}
	}
	process OrderCancelled <MarketplaceOrder> {
		roles {
        	OrderProcessor "TBD"
        	OrderObserver "TBD"
        
        }
	}
	
	workflow OrderCancelled implements OrderCancelled{
		 transaction OrderCancelled<MarketplaceOrder> {
			 roles(OrderProcessor, OrderObserver)
		 }	
	}
}